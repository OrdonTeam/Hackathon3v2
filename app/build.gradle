buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
    }
}

repositories {
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'codenarc'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            keyAlias 'OrdonTeam'
            storePassword '123456'
            keyPassword '123456'
        }
        release {
            storeFile file('debug.keystore')
            keyAlias 'OrdonTeam'
            storePassword '123456'
            keyPassword '123456'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    productFlavors {
        forCodeAnalyses {
            minSdkVersion 21
        }
        normal {
            testInstrumentationRunner "android.support.test.runner.AndroidSpockRunner"
        }
    }
    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }

    sourceSets.main {
        java.srcDirs = ['src/main/groovy', 'gen']
    }
}


dependencies {
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'org.codehaus.groovy:groovy:2.4.1:grooid'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided "com.squareup.dagger:dagger-compiler:1.2.2"

    //Don't know why it doesn't work without it
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.0')
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'org.hamcrest'
    }

    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.arasthel:swissknife:1.2.2'
}


task codenarc(type: CodeNarc) {
    configFile =  file("config/codenarc/codenarc.groovy")
    def allGroovySourceDirs = ['src/main/groovy','src/androidTest/groovy']
    exclude '**/*.java'

    allGroovySourceDirs.each {
        source(it)
    }
}

task codeAnalysesForCodeAnalyses
codeAnalysesForCodeAnalyses.dependsOn "codenarc"
codeAnalysesForCodeAnalyses.dependsOn "lintForCodeAnalysesDebug"
